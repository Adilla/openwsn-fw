import os

Import('env')

#============================ libbsppc ========================================
libbsppc_env = env.Clone()

libbsppc_env.Append(CPPPATH =  [ os.path.join('#','firmware','openos','bsp','boards'),
                               ],
                   )
                   
libbsppc_env.Append(CPPPATH =  [ os.path.join('#','firmware','openos','bsp','boards','pc'),
                               ],
                   )

libbsppc_source = ['board.c',
                   'bsp_timer.c',
                   'debugpins.c',
                   'eui64.c',
                   'leds.c',
                   'radio.c',
                   'radiotimer.c',
                   'uart.c',
                   'supply.c',
                  ]

libbsppc = libbsppc_env.Library(target='libbsppc',
                            source=libbsppc_source)
Alias('libbsppc', libbsppc)

#============================ opensim_client ==================================
opensim_client_env = env.Clone()

opensim_client_env.Append(LIBPATH =  [ os.path.join('#','firmware','openos','bsp','boards','pc'),],)

if opensim_client_env['PLATFORM'] == 'win32':
    opensim_client_env.Append(CPPPATH =  [ os.path.join('#','firmware','openos','bsp','boards','pc','win'),],)
    opensim_client_env.Append(LIBPATH =  [ os.path.join('#','firmware','openos','bsp','boards','pc','win'),],)
    opensim_client_sources  =  ['opensim_client.c','tcp_port_win.c']
    opensim_client_libs     =  ['libbsppc.lib','ws2_32.lib','user32.lib',]
    opensim_client = opensim_client_env.Program(
                    target  = 'opensim_client',
                    source  = opensim_client_sources,
                    LIBS    = opensim_client_libs,
                    )
    Alias('opensim_client', opensim_client)

else :
    opensim_client_env.Append(CPPPATH =  [ os.path.join('#','firmware','openos','bsp','boards','pc','linux'),],)
    opensim_client_env.Append(LIBPATH =  [ os.path.join('#','firmware','openos','bsp','boards','pc','linux'),],)
    opensim_client_sources_linux  =  ['opensim_client.c','tcp_port_linux.c']
    opensim_client_libs_linux     =  ['libbsppc.a']
    opensim_client = opensim_client_env.Program(
                    target  = 'opensim_client',
                    source  = opensim_client_sources_linux,
                    LIBS    = opensim_client_libs_linux,
                    )
    Alias('opensim_client', opensim_client)
